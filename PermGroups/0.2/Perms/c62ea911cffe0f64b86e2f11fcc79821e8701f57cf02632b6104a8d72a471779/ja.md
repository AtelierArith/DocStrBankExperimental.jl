このパッケージは、順列とそれに関するいくつかの関数を実装しています。これは、パッケージ `Combinat` のみを依存しています。

このパッケージは、GAP言語における順列の設計に従っています。`Perm` は、内部的に `1:n` の画像を保持する `n` 個の整数のベクトルとして表現される `1:n` の集合の順列です。整数 `n` は、順列の次数と呼ばれます。このパッケージでは、GAPと同様に（Magmaやパッケージ `Permutations.jl` の哲学とは対照的に）、異なる次数の2つの順列を掛け算することができます（結果はより大きい次数になります）。2つの順列は、同じ点を同じ方法で移動させる場合に限り等しいため、異なる次数の2つの順列が等しい場合もあります。したがって、次数は実装の詳細であり、通常は使用すべきではありません。むしろ、`last_moved` 関数を使用するべきです。

この設計により、異なるグループからの順列を掛け算することが容易になります。次数が固定されている設計と比較して、わずかなオーバーヘッドがあります。

順列のデフォルトコンストラクタは、`1:n` の画像のリストを使用します。例えば、`Perm([2,3,1,5,4,6])` のようにです。しばしば、サイクル分解を使用する方が便利です：上記の順列はサイクル分解 `(1,2,3)(4,5)` を持つため、`Perm(1,2,3)*Perm(4,5)` または `perm"(1,2,3)(4,5)"` と書くことができます（この最後の形式は、GAPからの順列やREPLでのデフォルトの印刷を解析できます）。`1:n` の画像のリストは、`perm` 関数を使用して順列から回復できます。異なる次数の等しい順列は異なる `perm` を持つことに注意してください。デフォルトコンストラクタは、`julia` 関数 `isperm` を呼び出すことで入力の有効性をテストします。入力をテストしないより高速なコンストラクタを使用するには、キーワード引数 `check=false` を使用してください。

順列の完全な型は `Perm{T}` であり、`T<:Integer` です。ここで、`Vector{T}` は `1:n` の画像を保持するベクトルの型です。これは、スペースや時間を節約するために使用できます。例えば、`Perm{UInt8}(1,2,3)` は、最大240の根を順列するため、ランク≤8のワイル群に使用できます。`T` が指定されていない場合、`Int16` として扱います。これは、速度、コンパクトさ、および `n` の可能なサイズの間の良い妥協です。異なる整数型の順列を混在させることができます。掛け算を行うと、より広い型に昇格されます。

# 順列を使った操作の例

```julia-repl
julia> a=Perm(1,2,3)
(1,2,3)

julia> perm(a)
3-element Vector{Int16}:
 2
 3
 1

julia> a==Perm(perm(a))
true

julia> b=Perm(1,2,3,4)
(1,2,3,4)

julia> a*b     # 積
(1,3,2,4)

julia> inv(a)  # 逆
(1,3,2)

julia> a/b     # 商 a*inv(b)
(3,4)

julia> a\b     # 左商 inv(a)*b
(1,4)

julia> a^b     # 共役 inv(b)*a*b
(2,3,4)

julia> b^2     # 二乗
(1,3)(2,4)

julia> 1^a     # 点1のaによる画像
2

julia> one(a)  # 自明な順列
()

julia> sign(a) # 順列の符号
1

julia> order(a) # 順列の順序（最小の自明な冪）
3

julia> last_moved(a)
3

julia> first_moved(a)
1

julia> Perm{Int8}(a) # aをPerm{Int8}に変換
Perm{Int8}: (1,2,3)

julia> Matrix(b)  # bの順列行列
4×4 Matrix{Bool}:
 0  1  0  0
 0  0  1  0
 0  0  0  1
 1  0  0  0
```

```julia-rep1
julia> randPerm(10) # 1:10のランダムな順列
(1,8,4,2,9,7,5,10,3,6)
```

`Perm` には `copy`、`hash`、`==` メソッドがあり、ハッシュのキーや集合の要素として使用できます。2つの順列は、同じ点を同じ画像に移動させる場合に等しいです。`cmp`、`isless` メソッド（移動した点の辞書順）を持っているため、ソートすることができます。`Perm` はブロードキャストのスカラーです。

順列に関する他のメソッドには `cycles`、`cycletype`、`reflection_length`、`mappingPerm`、`restricted`、`support`、`sortPerm`、`Perm_rowcol`、`preimage`、`randPerm` があります。

このパッケージには `Perm` を列挙するためのメソッドは提供されていません。`Combinat` の `permutations` メソッドを使用するか、`Combinat.Permutations` を反復するか、`PermGroups` の `symmetric_group` の要素を反復してください。
